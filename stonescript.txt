import UI/BetterInfo2
//import Cosmetics/PetDog
//import Cosmetics/PetCrab

>`0,1,@foe.state@, @foe.time@
>`0,2,@time.FormatCasual(totaltime)@
//>`0,3,Left:@item.left@
//>`0,4,Right:@item.right@
>`0,3,@foe.debuffs.string@

?hp<15
  activate potion

var dis=25

func move_pickup()
  ?pickup.distance<10
    equipL star
  :
    equipL ouroboros
  equipR triskelion

func bardiche_skill()
  ?foe.distance<=10 &
  ^foe.hp>52 &
  ^item.GetCooldown(＂bardiche＂)<=0 |
  ^item.GetCooldown(＂bardiche＂)>870
    equip bardiche
    activate R

func dodge()
  ?item.GetCooldown(＂mind＂)<=0
   equipL mind

func dash()
  ?foe.distance>10&item.GetCooldown(＂dash＂)<=0
    equipR dashing shield
    activate R

func blade_skill()
  ?(item.getCooldown(＂blade＂)<=0)
    equip blade
    activate R

func poison()
  ?foe.debuffs.string!∞
    equipL poison wand dp

func fire()
  ?foe.debuffs.string!φ
    equipR fire wand df

func ice()
  ?foe.debuffs.string!＂chill:6＂
    equipR ice wand

// Rocky Plateau ///////////////////////////////
?loc=rocky
/*
  ?harvest=Boulder & harvest.distance<15
    equip shovel

  ?foe=boss & foe.distance<20
    equipL stone
    equipR vigor shield
*/
  ?foe=phase1
    equipL vigor wand
    equipR poison wand
  :?foe=phase2
    equipL vigor wand
    equipR ice wand


// Deadwood Canyon /////////////////////////////
:?loc=deadwood
  ?foe=boss
    ?foe.name=＂Poena＂
      equipL vigor hammer dl
      equipR vigor hammer ah
      dash()
    ://tree
      equipL vigor hammer dl
      equipR vigor sword dl
      ice()
      poison()
      bardiche_skill()
      dash()
  ://not boss
    ?foe=immune_to_ranged
      equip heavy hammer
    ://ants and wasps
      equip repeat
    ?hp=maxhp
      dash()


// Caves of Fear ///////////////////////////////
:?loc=cave
  ?foe=boss
    equip bardiche
    bardiche_skill()
    poison()
    dash()
  ://not boss
    equipL ice wand
    equipR vigor wand
    dash()


// Mushroom Forest /////////////////////////////
:?loc=mushroom
  ?foe=boss
    ?foe.name=＂Mr. Puff＂
      equip repeat
      ?foe.distance<14
        dodge()
    :?foe.name=＂Big A** Snail＂
      equipL vigor hammer ah
      equipR vigor hammer dl
      dash()
    ://main bosses
      ?foe.name=＂Enoki＂
        equipL vigor sword
        equipR vigor hammer dl
      :?foe.name=＂Morel＂
        equipL vigor sword
        equipR vigor hammer dl
      :
        equipL vigor hammer ah
        equipR vigor hammer dl
        poison()
      dash()
  ://not boss
    ?foe.name=＂Ant＂&foe.count<=3
      equip repeat
      dash()
    ://not ants
      //equipL poison wand
      equipL ice wand
      equipR fire wand        
      ?hp=maxhp
        dash()


// Haunted Halls ///////////////////////////////
:?loc=halls
  ?foe=boss
    ?foe.name=＂R.I.Pieces＂
      equip repeat
    ://main boss
      ?foe=phase1
        equip vigor crossbow
        ?foe.state=32&foe.time>=65
          dodge()
      :?foe=phase2
        ?foe.debuffs.string!∞
          equipL poison wand dp
          equipR ice wand
        :
          equip vigor crossbow
  ://not boss
    equipL ice wand
    equipR vigor wand
    ?foe.name=＂Big Booo＂//&hp=maxhp
      dash()


// Boiling Mine ////////////////////////////////
:?loc=mine
  ?foe=boss
    ?foe.name=＂Bomb Cart＂
      equip ether crossbow
      ?foe.distance<8
        dodge()
    ://main boss
      ?(foe.state=32)&(foe.time=30)
          dodge()
      :?foe.state=33
        ?foe.armor>0
          equipL vigor hammer dl
          equipR vigor hammer ah
          bardiche_skill()
          dash()
        :
          equip ether crossbow
      :
        equip ether crossbow
  ://not boss
    equipL vigor wand
    equipR ice wand


// Icy Ridge ///////////////////////////////////
:?loc=icy
  ?foe=boss
    ?foe.name=＂Elemental＂
      equipL vigor sword
      equipR vigor hammer dl
      dash()
    ://main boss
      ?foe.state=33
//      ^|foe.debuffs.string!＂chill:6＂
        equipL ice wand
        equipR fire shield
      :
        equip fire crossbow

      ?foe.armor>0
        equipL vigor hammer dl
        equipR vigor sword
        dash()
  :?(foe=pillar|foe=wall)
    equipL vigor wand
    equipR vigor shield
  ://not boss
    ?foe.name=elemental
      equip repeat
    :
      equip fire crossbow

// Temple //////////////////////////////////////
:?loc=temple
  ?foe=boss
    equip bardiche
    bardiche_skill()
    ?foe.debuffs.string!∞
      equipL poison wand dp
      equipR ice wand
    ?foe.state=112&foe.time<60&foe.time=58
      dodge()
    :
      dash()
  ://not boss
    equipL ice wand
    equipR vigor wand
//    ?hp=maxhp
    dash()


// Postprocessing //////////////////////////////

?item.left.state=3
  equipL moon
?item.right.state=3&item.left!＂poison wand＂
  equipR moon

?foe.distance<25&foe.count>5&totaltime<1800
  blade_skill()

?foe.count=0 | foe.distance>dis
  move_pickup()

//Only for quests?//////////////////////////////

/*
?foe.name=＂Acronian Scout＂
  equip repeat
  bardiche_skill()

func steal()
  ?item.GetCooldown(＂skeleton_arm＂)<=0 & 
^foe.hp<=20 & foe ! immune_to_physical & 
^foe.armor<=0 & foe ! boss
    equip skeleton arm
    ?item.CanActivate(＂skeleton_arm＂)
       activate R
     >o-2,-3,#ffcccc,Shhh!~
steal()

func mask_skill()
  ?item.GetCooldown(＂mask＂)<=0
    ?foe.count>0&foe.distance<20//&foe=boss
      equip mask
      activate R
mask_skill()
*/